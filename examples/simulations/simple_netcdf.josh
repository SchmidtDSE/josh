start simulation TestSimpleSimulation

  grid.size = 1 count
  grid.low = 0 count latitude, 0 count longitude
  grid.high = 10 count latitude, 10 count longitude

  steps.low = 0 count
  steps.high = 10 count

  stepCount.init = 0 count
  stepCount.step = prior.stepCount + 1 count

  exportFiles.patch = "file:///tmp/simple_josh.nc"
  exportFiles.patch.columns = "averageAge,averageHeight"

end simulation


start patch Default

  ForeverTree.init = create ForeverTree

  export.averageAge.step = mean(ForeverTree.age)
  export.averageHeight.step = mean(ForeverTree.height)

end patch


start organism ForeverTree

  age.init = 0 year
  age.step = prior.age + 1 year
  assert.age.step:if(meta.stepCount == 5 count) = current.age == 5 years

  height.init = 0 meters
  height.step = prior.height + sample uniform from 0 meters to 1 meters

end organism


start unit year

  alias years
  alias yr
  alias yrs

end unit
